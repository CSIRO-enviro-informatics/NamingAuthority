== Naming Rule

This section describes the naming rule.

=== OGC name schemes

Two URI schemes [IETF RFC 3986] are defined by OGC to provide persistent names for resources of interest in geographic information infrastructures. The generic syntax for OGC names is described in [OGC Naming Authority – Procedures].

The generic syntax for OGC http URIs is

  URI = "http://www.opengis.net/" OGCResource "/" ResourceSpecificPath

The generic syntax for OGC URNs is [IETF RFC 5165]

  URN = "urn:ogc:" OGCResource ":" ResourceSpecificString

The following ABNF adapted from [IETF RFC 3986] provides some basic definitions required in the rest of this document.

  segment       = *pchar
  segment-nc    = *pchar-nc
  segment-nz    = 1*pchar
  segment-nz-nc = 1*pchar-nc
  pchar         = unreserved / pct-encoded / sub-delims / ":" / "@"
  pchar-nc      = unreserved / pct-encoded / sub-delims / "@"
  pct-encoded   = "%" HEXDIG HEXDIG
  unreserved    = ALPHA / DIGIT / "-" / "." / "_" / "~"
  reserved      = gen-delims / sub-delims
  gen-delims    = ":" / "/" / "?" / "#" / "[" / "]" / "@"
  sub-delims    = "!" / "$" / "&" / "'" / "(" / ")"
                  / "*" / "+" / "," / ";" / "="

=== Production rule for specification element names

An OGC name for a document or document element shall be produced using the following rule:

  OGCResource   = "doc"
  ResourceSpecificPath = doc-type "/" doc-identifier [ ( "#" / “/” ) doc-element]
  ResourceSpecificString = doc-type ":" doc-identifierURN [":" doc-elementURN]
  doc-type      = segment-nz-nc; token from the register of OGC document types.
  doc-identifier = doc-name ["/" doc-version]
  doc-identifierURN = doc-name [":" doc-version]
  doc-name      = segment-nz-nc ; acronym or code for document topic
  doc-version   = 1*DIGIT "." 1*DIGIT ["." 1*DIGIT]
  doc-element   = doc-element-type ( "-" / "/") element-number
  doc-elementURN   = doc-element-type ":" element-number
  doc-element-type = segment-nz-nc; token from the register of OGC document element types
  element-number = 1*DIGIT *("." 1*DIGIT)

The register of OGC document types can be found at http://uwww.opengis.net/register/ogc-na/doc-type.

The register of OGC document element types can be found at  http://uwww.opengis.net/register/ogc-na/doc-element-type.

*NOTE:* The document element (clause, table, figure, etc) is optional, If present, in the http URI form the element designator follows either a ‘/’ or ‘#’, and sub-elements may be separated by either ‘/’ or ‘-‘. If the ‘#’ form is used as a fragment separator resolution of the secondary resource is delegated to the http client. If the ‘#’ form is used as a fragment separator, the ‘-‘ is recommended as the separator for sub-elements.
